*** Settings ***
Documentation       To Verify Importing of CrunchyDB Provider Account and deployment of Database Instance
Metadata            Version    0.0.1

Library             SeleniumLibrary
Resource            ../resources/keywords/deploy_application.resource

Suite Setup         Set Library Search Order    SeleniumLibrary
Suite Teardown      Tear Down The Test Suite
Test Setup          Given Setup The Test Case
Test Teardown       Tear Down The Test Case

Force Tags          UI    crunchy


*** Test Cases ***
Scenario: Import CrunchyDB Provider Account From Developer View
    [Tags]    RHODA-020    smoke
    When User Navigates To Add Topology Screen From Developer View    Crunchy Bridge
    And User Navigates To Import Provider Account Screen From Developer View
    And User Enters Data To Import CrunchyDB Provider Account
    Then Provider Account Import Success

Scenario: Verify error message for invalid credentials on CrunchyDB
    [Tags]    RHODA-021    smoke
    When User Filters Project ${operatorNamespace} On Project DropDown And Navigates To Database Access Page
    And User Navigates To Import Database Provider Account Screen From Database Access Page
    And User Enters Invalid Data To Import CrunchyDB Provider Account
    Then Provider Account Import Failure

Scenario: Import CrunchyDB Provider Account From Administrator View
    [Tags]    RHODA-022
    When User Filters Project ${operatorNamespace} On Project DropDown And Navigates To Database Access Page
    And User Navigates To Import Database Provider Account Screen From Database Access Page
    And User Enters Data To Import CrunchyDB Provider Account
    Then Provider Account Import Success

Scenario: Deploy CrunchyDB Database Instance
    [Tags]    RHODA-023
    Skip If    "${PREV_TEST_STATUS}" == "FAIL"
    When User Imports Valid Crunchy Bridge Provider Account
    And User Navigates To Add Crunchy Bridge To Topology Screen
    And User Selects Database Instance For The Provider Account
    Then DBSC Instance Deployed On Developer Topology Graph View

Scenario: Connect CrunchyDB DBSC With An Openshift Application
    [Tags]    RHODA-024    smoke
    Skip If    "${PREV_TEST_STATUS}" == "FAIL"
    When User Deploys Crunchy Database Instance On Developer Topology Screen
    And User Imports Openshift crunchy Application From YAML
    And User Creates Service Binding Between crunchy DBSC Instance And Imported Openshift Application
    Then The Application Accesses The Connected crunchy Database Instance
